#!/bin/sed -Enf
:START
/[0-9]+\s*[+]\s*[0-9]+/{
    h
    b MATCHED
}
b END
:MATCHED
#TOTO JE NESKUTOCNY ODRB, ALE NEPRISIEL SOM NA TO AKO UROBIT TEN REGEXP, BEZ LAZY KVANTIFIKATORA
s/([0-9]+\s*[+]\s*[0-9]+)/;;;\1/
s/^.*[^;]*;([0-9]+)\s*[+]\s*([0-9]+).*$/\1 \2/

s/(^.*$)/,\1/

:BOTH
/^.*,[0-9]+\s[0-9]+$/{
    s/(.*)(,)([0-9]*)([0-9])(\s)([0-9]*)([0-9])/\1\4\7\2\3\5\6/
    b BOTH
}
:FIRST
/,[0-9]+/{
    s/(.*)(,)([0-9]*)([0-9])/\1\40\2\3/
    b FIRST
}
:SECOND
/,\s[0-9]+/{
    s/(.*)(,\s)([0-9]*)([0-9])/\10\4\2\3/
    b SECOND
}
s/(.*)(,\s)/\1/
s/([0-9]{2})([0-9]*)/,0\1:\2/
:ADD
:NUMTOX
/,.*0.*:/{
    s/(,.*)0(.*:)/\1\2/
    b NUMTOX
}
/,.*1.*:/{
    s/(,.*)1(.*:)/\1X\2/
    b NUMTOX
}
/,.*2.*:/{
    s/(,.*)2(.*:)/\1XX\2/
    b NUMTOX
}
/,.*3.*:/{
    s/(,.*)3(.*:)/\1XXX\2/
    b NUMTOX
}
/,.*4.*:/{
    s/(,.*)4(.*:)/\1XXXX\2/
    b NUMTOX
}
/,.*5.*:/{
    s/(,.*)5(.*:)/\1XXXXX\2/
    b NUMTOX
}
/,.*6.*:/{
    s/(,.*)6(.*:)/\1XXXXXX\2/
    b NUMTOX
}
/,.*7.*:/{
    s/(,.*)7(.*:)/\1XXXXXXX\2/
    b NUMTOX
}
/,.*8.*:/{
    s/(,.*)8(.*:)/\1XXXXXXXX\2/
    b NUMTOX
}
/,.*9.*:/{
    s/(,.*)9(.*:)/\1XXXXXXXXX\2/
    b NUMTOX
}
/,X{10}X*:/{
    s/,X{10}(X*):/,\11:/
    b ONE
}
s/,(X*):/,\10:/
:ONE
/,[^X]*:/{
    s/,(.*):/,0\1:/
}
/,X[0-9]:/{
    s/,X([0-9]):/,1\1:/
}
/,X{2}[0-9]:/{
    s/,X{2}([0-9]):/,2\1:/
}
/,X{3}[0-9]:/{
    s/,X{3}([0-9]):/,3\1:/
}
/,X{4}[0-9]:/{
    s/,X{4}([0-9]):/,4\1:/
}
/,X{5}[0-9]:/{
    s/,X{5}([0-9]):/,5\1:/
}
/,X{6}[0-9]:/{
    s/,X{6}([0-9]):/,6\1:/
}
/,X{7}[0-9]:/{
    s/,X{7}([0-9]):/,7\1:/
}
/,X{8}[0-9]:/{
    s/,X{8}([0-9]):/,8\1:/
}
/,X{9}[0-9]:/{
    s/,X{9}([0-9]):/,9\1:/
}

/:[0-9]+/{
    s/(^.*),([0-9])([0-9]):([0-9]{2})(.*$)/\2\1,\3\4:\5/
    b ADD
}
/:$/{
    s/(.*),([0-9])([0-9]):/\3\2\1/
    s/^0(.*$)/\1/
}
s/(.*)/\1,/
G
s/\n//
#TIEZ TEN ISTY ODRB
s/([0-9]+\s*[+]\s*[0-9]+)/;;;\1/
s/([0-9]+),(.*);;;([0-9]+)\s*[+]\s*([0-9]+)/\2\1/

b START

:END
p